dnl
dnl   /**------- <| --------------------------------------------------------**
dnl    **         A                  Clan/Scop                              **
dnl    **---     /.\   -----------------------------------------------------**
dnl    **   <|  [""M#               configure.in                            **
dnl    **-   A   | #   -----------------------------------------------------**
dnl    **   /.\ [""M#         First version: 30/04/2008                     **
dnl    **- [""M# | #  U"U#U  -----------------------------------------------**
dnl         | #  | #  \ .:/
dnl         | #  | #___| #
dnl ******  | "--'     .-"  ***************************************************
dnl *     |"-"-"-"-"-#-#-##   Clan : the Chunky Loop Analyser (experimental)  *
dnl ****  |     # ## ######  **************************************************
dnl *      \       .::::'/                                                    *
dnl *       \      ::::'/     Copyright (C) 2008 Cedric Bastoul               *
dnl *     :8a|    # # ##                                                      *
dnl *     ::88a      ###      This is free software; you can redistribute it  *
dnl *    ::::888a  8a ##::.   and/or modify it under the terms of the GNU     *
dnl *  ::::::::888a88a[]:::   Lesser General Public License as published by   *
dnl *::8:::::::::SUNDOGa8a::. the Free Software Foundation, either version 2.1*
dnl *::::::::8::::888:Y8888::                of the License, or (at your      *
dnl *::::':::88::::888::Y88a::::::::::::...  option) any later version.       *
dnl *::'::..    .   .....   ..   ...  .                                       *
dnl * This software is distributed in the hope that it will be useful, but    *
dnl * WITHOUT ANY WARRANTY; without even the implied warranty of              *
dnl * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU       *
dnl * General Public License  for more details.	                              *
dnl *                                                                         *
dnl * You should have received a copy of the GNU Lesser General Public        *
dnl * License along with software; if not, write to the Free Software         *
dnl * Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307    *
dnl * USA                                                                     *
dnl *                                                                         *
dnl * Clan, the Chunky Loop Analyser                                          *
dnl * Written by Cedric Bastoul, Cedric.Bastoul@inria.fr                      *
dnl *                                                                         *
dnl ***************************************************************************/
dnl
dnl Input file for autoconf to build a configuration shellscript.


AC_PREREQ(2.13)
dnl Fill here the @bug email adress.
AC_INIT([scoplib], [0.2.0], [cedric.bastoul@inria.fr,pouchet@cse.ohio-state.edu])
dnl A common file, which serve as a test.
AC_CONFIG_SRCDIR([include/scoplib/macros.h])
dnl Put as most as possible configuration files to an auxialiry
dnl directory.
AC_CONFIG_AUX_DIR([autoconf])
dnl Initialize automake. Here, a special tar version that enables
dnl (very) long filenames.
AM_INIT_AUTOMAKE([1.9 tar-ustar no-define foreign dist-bzip2])


dnl default version
BITS="64"
SCOPLIB_INT_T=SCOPLIB_INT_T_IS_LONGLONG


dnl /**************************************************************************
dnl  *                              Checking                                  *
dnl  **************************************************************************/


dnl Checks for programs.
AC_PROG_CC
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_CHECK_PROG(CD, cd)
AC_PROG_LIBTOOL
AC_CHECK_PROGS(DOXYGEN,doxygen,doxygen)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([errno.h stddef.h stdlib.h string.h strings.h unistd.h])

dnl Checks for library functions.
AC_CHECK_FUNCS(strtol)


dnl /**************************************************************************
dnl  *                             Option setting                             *
dnl  **************************************************************************/

dnl Some default values cause I'm not sure whether autoconf set them, while
dnl documentation says it does...
gmp_package="yes"
gmp_include_package="yes"
gmp_library_package="yes"

NEED_MP="no"

dnl --with-gmp=gmp-path
AC_ARG_WITH(gmp,
        [  --with-gmp=DIR          DIR where the gmp package is installed],
	[ echo "Package gmp : $withval" &&
	  gmp_package=$withval &&
          GMP_INC=$gmp_package/include &&
          GMP_LIB=$gmp_package/lib &&
          SCOPLIB_INT_T=SCOPLIB_INT_T_IS_MP &&
          NEED_MP="yes"])

AC_ARG_WITH(gmp-include,
        [  --with-gmp-include=DIR  DIR where gmp.h is installed],
	[ echo "Package gmp-include : $withval" &&
	  gmp_include_package=$withval &&
          GMP_INC=$gmp_include_package &&
          SCOPLIB_INT_T=SCOPLIB_INT_T_IS_MP &&
	  NEED_MP="yes"])

AC_ARG_WITH(gmp-library,
        [  --with-gmp-library=DIR  DIR where the gmp library is installed],
	[ echo "Package gmp-library : $withval" &&
	  gmp_library_package=$withval &&
          GMP_LIB=$gmp_library_package &&
          SCOPLIB_INT_T=SCOPLIB_INT_T_IS_MP &&
	  NEED_MP="yes"])

AC_ARG_ENABLE(int-version,
	[  --enable-int-version    'int' (32 bits) version is built],
	[ echo "Package int : $enableval" &&
	  BITS="32" &&
          SCOPLIB_INT_T=SCOPLIB_INT_T_IS_LONG])

AC_ARG_ENABLE(llint-version,
	[  --enable-llint-version  'long long int' (64 bits) version is built],
	[ echo "Package long long int : $enableval" &&
	  BITS="64" &&
          SCOPLIB_INT_T=SCOPLIB_INT_T_IS_LONGLONG])

AC_ARG_ENABLE(mp-version,
	[  --enable-mp-version     'MP' (multiple precision) version is built],
	[ echo "Package mp : $enableval" &&
	  BITS="MP" &&
          SCOPLIB_INT_T=SCOPLIB_INT_T_IS_MP &&
          CPPFLAGS="$CPPFLAGS -DLINEAR_VALUE_IS_MP"
          NEED_MP="yes"])

dnl FIXME: should be removed...
CPPFLAGS="$CPPFLAGS -D$SCOPLIB_INT_T"

dnl /**************************************************************************
dnl  *                            Where is GMP?                               *
dnl  **************************************************************************/


dnl Checking for gmp
AC_MSG_CHECKING(whether gmp works)
if test "$gmp_package" = "no"; then
  echo "GMP package not defined"
  AC_MSG_RESULT(no)
  TO_BUILD_MP=""
else
  if test "$NEED_MP" = "no"; then
    echo "Mode normal GMP"
    TO_BUILD="$TO_BUILD MP"
    AC_CHECK_HEADER(gmp.h,
                    [AC_CHECK_LIB(gmp,
		                  __gmpz_init,
		                  [LIBS="$LIBS -lgmp"],
				  [echo "Can't find gmp library." &&
				   echo "MP version will not be builded." &&
				   TO_BUILD_MP=""])],
		    [echo "Can't find gmp headers." &&
		     echo "MP version will not be builded." &&
		     TO_BUILD_MP=""])
  else
    dnl Default given by --with-X is "yes", --without-X is "no". We also
    dnl initialized manually all gmp_package* variables to "yes" (thus they are
    dnl supposed to be "yes" except if the user set them himself).

    if test "$gmp_package" != "yes" ; then
      echo "(GMP path has been set by user)"
      GMP_DIR=$gmp_package
      dnl Useful for AC_CHECK_X to find what we want.
      CPPFLAGS="-I$GMP_DIR/include $CPPFLAGS"
      LDFLAGS="-L$GMP_DIR/lib $LDFLAGS"
    fi

    if test "$gmp_include_package" != "yes" ; then
      CPPFLAGS="-I$GMP_INC $CPPFLAGS"
    fi

    if test "$gmp_library_package" != "yes" ; then
      LDFLAGS="-L$GMP_LIB $LDFLAGS"
    fi

    AC_CHECK_HEADER(gmp.h,
                    [],
                    [AC_MSG_ERROR(Can't find gmp headers.)])
    AC_CHECK_LIB(gmp,
                 __gmpz_init,
                 [LIBS="$LIBS -lgmp"],
                 [AC_MSG_ERROR(Can't find gmp library.)])

    AC_MSG_RESULT(yes)
  fi
fi


dnl /**************************************************************************
dnl  *                            Substitutions                               *
dnl  **************************************************************************/


dnl Substitutions to do.
AC_SUBST(BITS)
AC_SUBST(SCOPLIB_INT_T)
AC_SUBST(ac_aux_dir)

dnl Configure Makefiles.
AC_CONFIG_FILES([
	Makefile
	doc/Makefile
	doc/Doxyfile
	include/Makefile
	include/scoplib/scop.h
	source/Makefile
	],
	[test -z "$CONFIG_HEADERS" || echo timestamp > source/stamp-h.in])

AC_OUTPUT


echo "             /*-----------------------------------------------*"
echo "              *        Clan/Scop configuration is OK          *"
echo "              *-----------------------------------------------*/"
echo "It appears that your system is OK to start Clan/Scop compilation. You "
echo "need now to type \"make\". Lastly type \"make install\" to install Clan "
echo "on your system (log as root if necessary)."
